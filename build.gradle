plugins {
    id 'application'
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'uk.gov.hmcts.java' version '0.12.67'
    id 'com.github.ben-manes.versions' version '0.53.0'
    id 'com.gorylenko.gradle-git-properties' version '2.5.3'
    id 'info.solidsoft.pitest' version '1.15.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'au.com.dius.pact' version '4.6.17'
    id 'net.serenity-bdd.serenity-gradle-plugin' version '4.2.34'
    id 'org.sonarqube' version '7.0.0.6105'
    id 'project-report'
    id 'jacoco'
}


gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/src/main/resources/uk/gov/hmcts/probate/services/submit")
    keys = ['git.commit.id', 'git.commit.time']
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "GMT"
}

application {
    mainClass = 'uk.gov.hmcts.probate.SubmitApplication'
    group = 'uk.gov.hmcts.probate'
    version = "4.1.0"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jacoco {
    toolVersion = '0.8.14'
}

sonarqube {
    properties {
        property "sonar.exclusions", "**/RegistryConfig.java,**/SecurityConfiguration.java"
        property "sonar.projectName", "probate :: submit service"
        property "sonar.jacoco.reportPath", "${project.rootDir}/jacoco/test.exec"
        property "sonar.host.url", "https://sonar.reform.hmcts.net/"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

project.ext {
  pacticipant = 'probate_submitService'
  pacticipantVersion = getCheckedOutGitCommitHash()
}

test.finalizedBy jacocoTestReport

dependencyCheck {
    suppressionFile = "${project.rootDir}/config/owasp/suppressions.xml"
}

sourceSets {
    testFunctional {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/functionalTest/java')
        }
        resources.srcDir file('src/functionalTest/resources')
    }

    testSmoke {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/smokeTest/java')
        }
        resources.srcDir file('src/smokeTest/resources')
    }
    testContract {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/contractTest/java')
        }
        resources.srcDir file('src/contractTest/resources')
    }
    testPact {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/pactTest/java')
        }
        resources.srcDir file('src/pactTest/resources')
    }
    integrationTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

tasks.register('integration', Test) {
    description = "Runs integration tests"
    group = "Verification"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

tasks.register('functional', Test) {
    description = "Runs functional Tests"
    testClassesDirs = sourceSets.testFunctional.output.classesDirs
    classpath = sourceSets.testFunctional.runtimeClasspath
    finalizedBy aggregate
}

functional.finalizedBy(aggregate)


tasks.register('pactTask', Test) {
    description = "Runs pact Tests"
    testClassesDirs = sourceSets.testPact.output.classesDirs
    classpath = sourceSets.testPact.runtimeClasspath
}

tasks.register('smoke', Test) {
    description = "Runs Smoke Tests"
    testClassesDirs = sourceSets.testSmoke.output.classesDirs
    classpath = sourceSets.testSmoke.runtimeClasspath
}

tasks.register('fortifyScan', JavaExec) {
    getMainClass().set("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

tasks.withType(Test).configureEach {
    maxHeapSize = "1024m"
    useJUnitPlatform()
}

jacocoTestReport {
    executionData(test, integration)
    reports {
        xml.required = true
        csv.required = false
        xml.outputLocation = file("${project.rootDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

check.dependsOn(integration)

pitest {
    targetClasses = ['uk.gov.hmcts.probate.*']
    threads = 4
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 50
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1' }
}

def versions = [
        applicationInsightsVersion      : '3.7.5',
        authCheckerLib                  : '3.1.1',
        ccdClient                       : '5.2.0',
        feignHttpClient                 : '13.6',
        httpclient5                     : '5.5.1',
        jackson                         : '2.20.0',
        junitJupiter                    : '5.12.2',
        lombok                          : '1.18.42',
        logging                         : '6.1.9',
        pact_version                    : '4.1.34',
        probateCommons                  : '2.2.12',
        restAssured                     : '5.5.6',
        serenity                        : '4.2.34',
        serviceAuthProviderClient       : '5.3.3',
        springBoot                      : '3.5.6',
        springCloudStarterNetflixHystrix: '4.3.0',
        springCloudWiremock             : '4.3.0',
        springDocUi                     : '2.8.13',
        swaggerAnnotations              : '2.2.39',
        springSecurityTesting           : '6.5.5',
        fortifyClient                   : '1.4.10',
        tomcat                          : '11.0.13'
]

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2025.0.0'
    }
}

dependencies {
    implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.11.0'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBoot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf', version: versions.springBoot
    implementation group: 'org.springframework.retry', name: 'spring-retry', version: '2.0.12'
    compileOnly group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: versions.springBoot
    compileOnly group:'org.projectlombok', name: 'lombok', version: versions.lombok
    annotationProcessor group:'org.projectlombok', name:'lombok', version: versions.lombok
    implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: versions.feignHttpClient
    implementation group: 'io.github.openfeign', name: 'feign-hc5', version: versions.feignHttpClient
    implementation group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: versions.httpclient5
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: versions.springCloudStarterNetflixHystrix
    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: versions.springDocUi
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: versions.swaggerAnnotations
    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.logging
    implementation group: 'com.github.hmcts', name: 'auth-checker-lib', version: versions.authCheckerLib
    implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: versions.serviceAuthProviderClient
    implementation group: 'com.github.hmcts', name: 'core-case-data-store-client', version: versions.ccdClient
    implementation group: 'com.github.hmcts', name: 'probate-commons', version: versions.probateCommons
    implementation group: 'com.microsoft.azure', name: 'applicationinsights-core', version: versions.applicationInsightsVersion
    implementation group: 'com.microsoft.azure', name: 'applicationinsights-runtime-attach', version: versions.applicationInsightsVersion
    implementation group: 'org.pitest', name: 'pitest', version: '1.21.0'
    implementation group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: '1.15.0'
    implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '8.1'
    implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.2.2'

    //Solves CVE-2025-48989
    implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: versions.tomcat
    implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-el', version: versions.tomcat
    implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: versions.tomcat

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot
    testImplementation group: 'org.springframework.security', name: 'spring-security-test', version: versions.springSecurityTesting
    testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: versions.springCloudWiremock

    testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: versions.fortifyClient, classifier: 'all'
    testImplementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: versions.serviceAuthProviderClient
    testImplementation group: 'com.github.hmcts', name: 'auth-checker-lib', version: versions.authCheckerLib
    testImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junitJupiter
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junitJupiter
    testRuntimeOnly group: 'com.github.hmcts', name: 'auth-checker-lib', version: versions.authCheckerLib
    testRuntimeOnly group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: versions.serviceAuthProviderClient

    testFunctionalImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
    testFunctionalImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
    testFunctionalImplementation group: 'net.serenity-bdd', name: 'serenity-junit5', version: versions.serenity
    testFunctionalImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
    testFunctionalImplementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.13.0'
    testFunctionalImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
    testFunctionalCompileOnly group:'org.projectlombok', name: 'lombok', version: versions.lombok
    testFunctionalAnnotationProcessor group:'org.projectlombok', name: 'lombok', version: versions.lombok
    testFunctionalImplementation sourceSets.main.runtimeClasspath
    testFunctionalImplementation sourceSets.test.runtimeClasspath

    testSmokeImplementation group: 'net.serenity-bdd', name: 'serenity-junit5', version: versions.serenity
    testSmokeImplementation sourceSets.main.runtimeClasspath
    testSmokeImplementation sourceSets.test.runtimeClasspath

    testContractImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
    testContractImplementation group: 'net.serenity-bdd', name: 'serenity-junit5', version: versions.serenity
    testContractImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
    testContractImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
    testContractImplementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.13.0'
    testContractImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
    testContractImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    testContractImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
    testContractImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.20'
    testContractCompileOnly group:'org.projectlombok', name: 'lombok', version: versions.lombok
    testContractAnnotationProcessor group:'org.projectlombok', name: 'lombok', version: versions.lombok
    testContractImplementation sourceSets.main.runtimeClasspath
    testContractImplementation sourceSets.test.runtimeClasspath

    testPactImplementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.13.0'
    testPactImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot
    testPactImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junitJupiter
    testPactImplementation group: 'com.github.hmcts', name: 'probate-pact-commons', version: versions.probateCommons
    testPactImplementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.5.0'
    testPactImplementation group: 'au.com.dius.pact.provider', name: 'junit5', version: versions.pact_version
    testPactImplementation group: 'au.com.dius.pact.provider', name: 'spring', version: versions.pact_version
    testPactImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
    testPactImplementation group: 'au.com.dius.pact.consumer', name: 'java8',  version: versions.pact_version
    testPactRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
    testPactRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junitJupiter
    testPactRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.13.2'
    testPactImplementation sourceSets.main.runtimeClasspath
    testPactImplementation sourceSets.test.runtimeClasspath

    integrationTestImplementation sourceSets.main.runtimeClasspath
    integrationTestImplementation sourceSets.test.runtimeClasspath
}

configurations.configureEach {
    resolutionStrategy {
        eachDependency { details ->
            // Resolve CVE‑2025‑48976
            if (details.requested.name == 'commons-fileupload') {
                details.useVersion '1.6.0'
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << '-parameters'
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

ext['snakeyaml.version'] = '2.2'

bootJar {
    archiveFileName = 'submit-service.jar'
    manifest {

        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version
    }
}

tasks.register('printVersion') {
    doLast {
        print project.version
    }
}

run {
    if (new Boolean(System.getenv("POINT_TO_AAT"))) {
        println 'Exporting AAT env vars--->'
        println 'Exporting AAT env vars' +project.file('./.aat-env').exists()
        if (project.file('./.aat-env').exists()) {
            project.file('./.aat-env').readLines().each() {
                def index = it.indexOf("=")
                def key = it.substring(0, index)
                def value = it.substring(index + 1)
                println 'key:' + key +'--value:' +value
                environment key, value
            }
        }
    }
    if (debug == 'true') {
        jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005']
    }
}

tasks.register('runAndPublishConsumerPactTests', Test) {
    description = "Runs consumer pact Tests"
    testClassesDirs = sourceSets.testPact.output.classesDirs
    classpath = sourceSets.testPact.runtimeClasspath
    include "uk/gov/hmcts/probate/services/submit/controllers/v2/consumer/**"

}

pact {
  broker {
      pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?:'http://localhost:80'
  }
  publish {
    pactDirectory = 'pacts'
    tags = [System.getenv("PACT_BRANCH_NAME") ?:'Dev']
    version = project.pacticipantVersion
  }
}
runAndPublishConsumerPactTests.finalizedBy pactPublish

static def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

rootProject.tasks.named("processResources") {
    dependsOn("generateGitProperties")
}
